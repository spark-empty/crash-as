/*==================================================================================================

   @file        autosar_flash.ld
   @version     2.0.0

   @brief       DIAB Linker Command File for Internal FLASH for XPC564XA
   @details     DIAB Linker Command File for Internal FLASH for XPC564XA

   Project      : AUTOSAR 3.0 MCAL
   Patform      : PA
   Peripheral   : 
   Dependencies : Base Det Dem Port Dio Mcu SchM Wdg WdgIf CanIf LinIf EcuM

   Autosar Version       : 1.9.0
   Autosar Revision      : ASR_REL_1_9_REV_0003
   Autosar Conf. Variant :
   Software Version      : 2.0.0
   Build Version         : MPC563xMxM_MCAL_2_0_0_RTM_ASR_REL_1_9_REV_0003_20110214

   (c) Copyright 2006-2011 Freescale Semiconductor Inc. & STMicroelectronics
   All Rights Reserved.

===================================================================================================

Note: This linker command file to be used with XPC564XA devices only.
==================================================================================================*/

MEMORY {

    /* XPC563XM - Flash 1.5 MB - 0x00 - 0x17FFFF */

    flash_rsvd1       : org = 0x00000000, len = 8
    flash_memory      : org = 0x00000008, len = 0xEFFF8
    flash_vec         : org = 0x000F0000, len = 0x90000
    flash_rsvd2       : org = 0x00180000, len = 0

    /* XPC563XM - SRAM 94 KB : 0x40000000 - 0x400177FF */

    ram_rsvd1    :     org = 0x20000000, len = 0
    int_sram     :     org = 0x40000000, len = 0x163A0
    res_ram      :     org = 0x400163A0, len = 0x20      /* a reserved ram section needed by FLS driver */
    iram_stack   :     org = 0x400163C0, len = 0x01000
    int_timers   :     org = 0x400173C0, len = 0x0340
    int_results  :     org = 0x40017700, len = 0x100
}


/*
 Program layout for starting in ROM, copying data to RAM,
 and continuing to execute out of ROM.
*/

SECTIONS
{

/*----------------*/
/* SECTIONS    */
/*----------------*/
      GROUP : {
           .rcw : {}
    }>flash_rsvd1

      GROUP : {
        .text : {}
        .text_vle (TEXT) : {
            *(.text)
            *(.text_vle)
         }

        .rodata(TEXT) :{}
        .sdata2 (DATA) : {}
        .fixaddr : {}
        .fixtype : {}
        .align_section ALIGN(16): {}
        _load_ramcode = . ; /* The start of the flash region rezerved for ramcode section and for the .sdata0, .sdata and .data sections.*/
    }>flash_memory

     GROUP  : {
            .isrvectbl(TEXT) : {}
            .isrvectbl_core(TEXT) : {}
    }>flash_vec

      GROUP : {
                 /* map consecutively .ramcode, .sdata0, sdata and .data sections */
                .ramcode(TEXT) ALIGN(4) LOAD(_load_ramcode) : {}
                .sdata0(DATA) ALIGN(4) LOAD( ADDR(.sdata0) - ADDR(.ramcode) + _load_ramcode) : {}
                .sdabase ALIGN(8) LOAD( ADDR(.sdabase) - ADDR(.ramcode) + _load_ramcode) : {}
                .sdata (DATA) LOAD( ADDR(.sdata) - ADDR(.ramcode) + _load_ramcode) : {}
                .data (DATA) LOAD( ADDR(.data) - ADDR(.ramcode) + _load_ramcode) : {}
              }>int_sram
      GROUP : {
                .sbss0(BSS) : {}
                .sbss (BSS) : {}
                .bss  (BSS) : {}
              }>int_sram




    /* The second section will allocate space for the initialized data
         * (.data/.sdata) and the unititialized data (.bss/.sbss) in the "ram" section.
    */

    GROUP : {
        .pbadc_cfg(DATA) : {}
    }> flash_memory

    GROUP : {
        .pbcan_cfg(DATA) : {}
    }> flash_memory

    GROUP : {
        .pbdio_cfg(DATA) : {}
    }> flash_memory

    GROUP : {
        .pbfls_cfg(DATA) : {}
    }> flash_memory

    GROUP : {
        .pbgpt_cfg(DATA) : {}
    }> flash_memory

    GROUP : {
        .pbicu_cfg(DATA) : {}
    }> flash_memory

    GROUP : {
        .pblin_cfg(DATA) : {}
    }> flash_memory

    GROUP : {
        .pbmcu_cfg(DATA) : {}
    }> flash_memory

    GROUP : {
        .pbport_cfg(DATA) : {}
    }> flash_memory

    GROUP : {
        .pbpwm_cfg(DATA) : {}
    }> flash_memory

    GROUP : {
        .pbspi_cfg(DATA) : {}
    }> flash_memory

    GROUP : {
        .pbwdg_cfg(DATA) : {}
    }> flash_memory

    GROUP : {
        .pbfr_cfg(DATA) : {}
    }> flash_memory

    __IV_ADDR           = ADDR(flash_vec);

    PB_ADC_CFG1         = ADDR(.pbadc_cfg);
    PB_CAN_CFG1         = ADDR(.pbcan_cfg);
    PB_DIO_CFG1         = ADDR(.pbdio_cfg);
    PB_FLS_CFG1         = ADDR(.pbfls_cfg);
    PB_GPT_CFG1         = ADDR(.pbgpt_cfg);
    PB_ICU_CFG1         = ADDR(.pbicu_cfg);
    PB_LIN_CFG1         = ADDR(.pblin_cfg);
    PB_MCU_CFG1         = ADDR(.pbmcu_cfg);
    PB_PORT_CFG1        = ADDR(.pbport_cfg);
    PB_PWM_CFG1         = ADDR(.pbpwm_cfg);
    PB_SPI_CFG1         = ADDR(.pbspi_cfg);
    PB_WDG_CFG1         = ADDR(.pbwdg_cfg);
    PB_FR_CFG1         = ADDR(.pbwdg_cfg);

    INT_SRAM_START      = ADDR(int_sram);
    INT_SRAM_SIZE       = 0x177FF/64;                   
/*    INT_SRAM_SIZE       = (SIZEOF(int_sram)+SIZEOF(iram_stack)+SIZEOF(int_timers)+SIZEOF(int_results))/4; */  /* Init 4 bytes at a time*/


    SBSS_SRAM_START     = ADDR(.sbss);
    SBSS_SRAM_SIZE      = (SIZEOF(.sbss)+3)/4;
    BSS_SRAM_START      = ADDR(.bss);
    BSS_SRAM_SIZE       = (SIZEOF(.bss)+3)/4;
    SDATA_RW_START  = _SDA_BASE_;
    SDATA_RO_START  = _SDA2_BASE_;

   /* generate symbols used in start-up code to init .ramcode section */
    RC_RAMCODE_SRC =  _load_ramcode;
    RC_RAMCODE_DEST = ADDR(.ramcode);
    RC_RAMCODE_SIZE = (SIZEOF(.ramcode)+3) / 4;

     /* generate symbols used in start-up code to init .sdata0 section */
    RC_PPC_SDATA2_SRC  = ADDR(.sdata0) - ADDR(.ramcode) + _load_ramcode;
    RC_PPC_SDATA2_DEST = ADDR(.sdata0);
    RC_PPC_SDATA2_SIZE = (SIZEOF(.sdata0)+3) / 4;

    /* generate symbols used in start-up code to init .sdata section */
    RC_SDATA_SRC       = ADDR(.sdata) - ADDR(.ramcode) + _load_ramcode;
    RC_SDATA_DEST      = ADDR(.sdata);
    RC_SDATA_SIZE      = (SIZEOF(.sdata)+3) / 4;

    /* generate symbols used in start-up code to init .data section */
    RC_DATA_SRC        = ADDR(.data) - ADDR(.ramcode) + _load_ramcode;
    RC_DATA_DEST       = ADDR(.data);
    RC_DATA_SIZE       = (SIZEOF(.data)+3) / 4;


    __SP_INIT           = ADDR(iram_stack) + SIZEOF(iram_stack);
    __SP_END            = ADDR(iram_stack);
    __STACK_SIZE        = SIZEOF(iram_stack);
    _ERASE_FUNC_ADDRESS_            = ADDR(res_ram);
    _WRITE_FUNC_ADDRESS_            = ADDR(res_ram);

}
